class DiagramaTeste

instance variables

public diag : Diagrama := new Diagrama("teste", 10);
public id: nat1 := 1;


operations
   -- operação auxiliar, que tira partido do facto do 
   -- interpretador parar quando se viola uma pré-condição
  public AssertTrue : bool ==> ()
  AssertTrue(a) == return
    pre a;
	
	
public reset: () ==> ()
reset() == 
	diag := new Diagrama("teste", 10);
	
	
public geradorId: () ==> nat1
geradorId() ==
	(
		dcl i: nat1 := id;
		id:= id + 1;
		return i;
	);
		

public TestInserirSegmento : () ==> ()
TestInserirSegmento () ==
	(
		dcl seg : Segmento := new Segmento("s", geradorId());
		dcl seg2: Segmento;
		dcl tamanho : nat := len diag.segmentos;
		diag.inserirSegmento(seg);
		seg2 := diag.getSegmento(len diag.segmentos);
		AssertTrue(seg.nome = seg2.nome);
		AssertTrue(seg.id = seg2.id);
		AssertTrue(tamanho+1 = len diag.segmentos);

	);

public TestInserirSegmentoOrdem: nat1 ==> ()
TestInserirSegmentoOrdem(ord) ==

	(
		dcl seg : Segmento := new Segmento("s", geradorId());
		dcl seg2: Segmento;
		dcl tamanho : nat := len diag.segmentos;
		diag.inserirSegmento(seg, ord);
		seg2 := diag.getSegmento(ord);
		AssertTrue(seg.nome = seg2.nome);
		AssertTrue(seg.id = seg2.id);
		AssertTrue(tamanho+1 = len diag.segmentos);
	);
	
public TestEditarSegmento: () ==> ()
TestEditarSegmento() ==
	(
		dcl seg : Segmento := new Segmento("s", geradorId());
		dcl seg2: Segmento;
		dcl tamanho : nat := len diag.segmentos;
		diag.inserirSegmento(seg, 1);
		
		diag.editarSegmento(1, "abc");
		
		seg2 := diag.getSegmento(1);
		AssertTrue(seg2.nome = "abc");
		AssertTrue(seg.id = seg2.id);
		AssertTrue(tamanho+1 = len diag.segmentos);
	);


public TestInserirObjeto : () ==> ()
TestInserirObjeto () ==
	(
		dcl obj : Objeto := new Objeto("nome1", geradorId(), "Actor");
		dcl obj2: Objeto;
		dcl tamanho : nat := len diag.objetos;
		diag.inserirObjeto(obj);
		obj2 := diag.getObjeto(len diag.objetos);
		AssertTrue(obj.nome = obj2.nome);
		AssertTrue(obj.id = obj2.id);
		AssertTrue(tamanho+1 = len diag.objetos);
	);

public TestEditarObjeto: () ==> ()
TestEditarObjeto() ==
	(
		dcl obj : Objeto := new Objeto("nome1", geradorId(), "Actor");
		dcl obj2: Objeto;
		dcl tamanho : nat := len diag.objetos;
		reset();
		diag.inserirObjeto(obj, 1);
		
		diag.editarObjeto("nome2", "Object", 1);
		
		obj2 := diag.getObjeto(1);
		AssertTrue(obj2.nome = "nome2");
		AssertTrue(obj.id = obj2.id);
		AssertTrue(obj2.tipo = "Object");
		AssertTrue(tamanho+1 = len diag.objetos);
	);
	


public TestEliminarSegmento : () ==> ()
TestEliminarSegmento () ==
	(
		dcl seg : Segmento := new Segmento("nome1", geradorId());
		dcl seg1: Segmento := new Segmento("nome2", geradorId());
		dcl seg11: Segmento := new Segmento("nome3", geradorId());

		dcl tamanho : nat;
		reset();
		diag.inserirSegmento(seg);
		diag.inserirSegmento(seg1);
		diag.inserirSegmento(seg11);
		tamanho := len diag.segmentos;
		
		
		diag.eliminarSegmento(1);
		
		for all s in set elems diag.segmentos do
			AssertTrue(s.id <> seg.id);
			
		AssertTrue(tamanho-1 = len diag.segmentos);
	);
	
public TestEliminarObjeto : () ==> ()
TestEliminarObjeto () ==
	(
		dcl obj : Objeto := new Objeto("nome1", geradorId(), "Actor");
		dcl obj1: Objeto := new Objeto("nome2", geradorId(), "Object");
		dcl obj11: Objeto := new Objeto("nome3", geradorId(), "Actor");
		
		dcl tamanho : nat;
		reset();
		diag.inserirObjeto(obj);
		diag.inserirObjeto(obj1);
		diag.inserirObjeto(obj11);
		tamanho := len diag.objetos;
		diag.eliminarObjeto(2);
		
		for all s in set elems diag.objetos do
			AssertTrue(s.id <> obj1.id);
			
		AssertTrue(tamanho-1 = len diag.objetos);
	);	

end DiagramaTeste